Actor NadeLauncherSpawner : CustomInventory
{
scale 0.8
Tag "Grenade Launcher"
Inventory.PickupMessage "You got the grenade launcher!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		GLAU A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		GLAU A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		GLAU A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64NadeLauncher",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64NadeLauncherTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64NadeLauncher")
		TNT1 A 0 A_SpawnItemEx("64NadeLauncherTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64NadeLauncher : DoomWeapon
{
Scale 0.8
Weapon.SlotNumber 5
Weapon.SelectionOrder 1500
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 2
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the grenade launcher!"
Obituary "%o caught %k's grenade."
Tag "Grenade Launcher"
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
+WEAPON.EXPLOSIVE
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		GLAU A -1
		Stop
	Ready:
		GRLG A 1 A_WeaponReady
		Loop
	Deselect:
		GRLG A 1 A_Lower
		Loop
	Select:
		GRLG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLF A 1 Bright A_FireCustomMissile("64NadeLauncherGrenades")
		TNT1 A 0 A_Recoil(2)
		GRLF BCDE 1 Bright
		GRLG C 2
		GRLG CBB 2
		GRLG A 8 A_WeaponReady
		GRLG A 1 A_Refire
		Goto Ready
	}
}

Actor 64NadeLauncherTACTICAL : Brutal64Weapon
{
Scale 0.8
Weapon.SlotNumber 5
Weapon.SelectionOrder 1500
Weapon.AmmoType "RocketAmmo"
Weapon.AmmoUse 1
Weapon.AmmoGive 2
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
+WEAPON.EXPLOSIVE
+WEAPON.ALT_USES_BOTH
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the grenade launcher!"
Obituary "%o caught %k's grenade."
Tag "Grenade Launcher"
+INVENTORY.RESTRICTABSOLUTELY
	States
	{
	Spawn:
		GLAU A -1
		Stop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		GRLS EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("Grenadier/Ready",CHAN_AUTO)
		GRLS ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		GRLG A 1 A_WeaponReady
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLF A 1 Bright A_FireCustomMissile("64NadeLauncherGrenades")
		TNT1 A 0 A_Recoil(2)
		GRLF B 1 Bright A_SetPitch(pitch-2,SPF_INTERPOLATE)
		GRLF C 1 Bright A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		GRLF D 1 Bright A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		GRLF E 1 Bright A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		GRLG C 2 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		GRLG CBB 2
		GRLG A 8 A_WeaponReady
		GRLG A 1 A_Refire
		Goto Ready
	AltFire:
		TNT1 A 0 A_PlaySound("Grenadier/Fire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle5",20,5,22)
		TNT1 A 0 A_FireCustomMissile("GunFireSmoke",0,0)
		GRLV A 1 A_FireCustomMissile("64NadeLauncherGasnades")
		TNT1 A 0 A_Recoil(2)
		GRLV B 1 A_SetPitch(pitch-2,SPF_INTERPOLATE)
		GRLV C 1 A_SetPitch(pitch+0.8,SPF_INTERPOLATE)
		GRLV D 1 A_SetPitch(pitch+0.6,SPF_INTERPOLATE)
		GRLV E 1 A_SetPitch(pitch+0.4,SPF_INTERPOLATE)
		GRLG C 2 A_SetPitch(pitch+0.2,SPF_INTERPOLATE)
		GRLG C 8
		GRLG CBB 2
		GRLG AA 8 A_WeaponReady
		Goto Ready
	MineThrowFlash:
		GRLS EFGH 1
		TNT1 A 18
		GRLS ABCD 1
		Stop
	KickingFlash:
		GRLS DCBA 1
		GRLS A 8
		GRLS ABCD 1
		Stop
	AirKickingFlash:
		GRLS DCBA 1
		GRLS A 10
		GRLS ABCD 1
		Stop
	SlideKickingStart:
		GRLS DCBA 1
		GRLS A 18
	SlideKickingEnd:
		GRLS A 6
		GRLS ABCD 1
		Stop
	}
}

Actor 64NadeLauncherGrenades : 64NightcrawlerGrenade
{
Damage 5
Speed 15
Species "Marines"
+THRUSPECIES
BounceSound "Grenadier/Bouncy"
}

Actor 64NadeLauncherGasnades : 64NightcrawlerGrenade
{
Damage 1
Speed 10
Species "Marines"
+THRUSPECIES
+FORCEPAIN
DeathSound "Grenadier/Gas"
BounceSound "Grenadier/Bouncy"
States
	{
	Spawn:
		GREN A 0 NoDelay ThrustThingZ(0,10,0,1)
	Spawn2:
		GREN AAAA 2 A_CustomMissile("GasnadeSmoke",0,0,0,2)
		Loop
	Death:
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,90)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,-90)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,180)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0)
		GREN A 50
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,90)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,-90)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0,180)
		TNT1 A 0 A_CustomMissile("PoisonShotCloud1",0,0)
	NadeOnFloor:
		GREN A 400
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode",1,"Stoping")
		Loop
	Stoping:
		TNT1 A 1
		Stop
	}
}

Actor GasnadeSmoke : OldschoolRocketSmokeTrail2
{
renderstyle stencil
stencilcolor DarkGreen
}