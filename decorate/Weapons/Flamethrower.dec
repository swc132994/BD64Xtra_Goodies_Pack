Actor FlamerWeaponSpawner : CustomInventory
{
scale 0.7
Tag "Flamethrower"
Inventory.PickupMessage "You got the flamethrower!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		WFLM A 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		WFLM A 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		WFLM A -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Flamethrower",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64FlamethrowerTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Flamethrower")
		TNT1 A 0 A_SpawnItemEx("64FlamethrowerTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor SawGasXtra : SawGas Replaces SawGas
{
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern","64DoomPlayer"
}

Actor 64Flamethrower : DoomWeapon
{
scale 0.7
Weapon.SlotNumber 9
Weapon.AmmoType "SawGas"
Weapon.AmmoGive 50
Weapon.AmmoUse 1
Inventory.PickupMessage "You got the flamethrower!"
Inventory.PickupSound "WPICK"
Obituary "%o was cremated by %k's flamethrower."
Tag "Flamethrower"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOAIM
+WEAPON.EXPLOSIVE
	States
	{
	Spawn:
		WFLM A -1
		Loop
	Ready:
		FLMG A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		FLMG A 1 A_Lower
		Loop
	Select:
		FLMG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_ZoomFactor(0.98)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlames",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects",0,0)
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 A 0 A_ZoomFactor(1.0)
		FLMG D 2 A_ReFire
		FLMG EA 2
		Goto Ready
	}
}

Actor 64FlamerFlames
{
Scale 0.67
Radius 2
Height 4
Speed 16
Damage 1
Alpha 0.67
RenderStyle Add
DamageType "Plasma"
SeeSound "Flamethrower/Fire"
DeathSound "Flamethrower/Hit"
Projectile
+DONTSPLASH
+NODAMAGETHRUST
-SOLID
	States
	{
	Spawn:
		FRFX A 2 NoDelay A_JumpIf(waterlevel > 1,"Splash")
		FRFX BCD 2 bright A_Explode(1,8,0)
		TNT1 A 0 A_LowGravity
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Splash")
		FRFX EFG 2 bright A_Explode(1,16,0)
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Splash")
		FRFX HIJ 2 bright A_Explode(1,32,0)
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Splash")
		FRFX KLM 2 bright A_Explode(1,64,0)
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Splash")
		FRFX NO 2 bright
		Stop
	Death:
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece2",random(-25,25),random(-15,15))
		TNT1 A 0 A_SpawnItemEx("TinyBurningPiece",random(-15,15),random(-15,15))
		TNT1 A 0 A_CustomMissile("FlamethrowerFireParticles",6,0,random(0,360),2,random(10,90))
		FRFX HIJ 2 bright A_Explode(1,32,0)
		FRFX KLM 2 bright A_Explode(1,64,0)
		FRFX NO 2 bright
		Stop
	Splash:
		TNT1 A 1
		Stop
	}
}
Actor 64FlamerFlamesLessEffects : 64FlamerFlames
{
	States
	{
	Death:
		TNT1 A 0 A_CustomMissile("FlamethrowerFireParticles",6,0,random(0,360),2,random(10,90))
		FRFX HIJ 2 bright A_Explode(1,32,0)
		FRFX KLM 2 bright A_Explode(1,64,0)
		FRFX NO 2 bright
		Stop
	}
}

Actor 64FlamerExploder : 64FatShot
{
Radius 4
Height 4
Damage 50
Speed 30
RenderStyle Add
Alpha 0.67
+THRUSPECIES
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(waterlevel > 1,"Death")
		MTB2 A 2 Bright A_SpawnItemEx("YellowFlare")
		TNT1 A 0 A_SpawnItemEx("MancubusFireTrailparticle")
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Death")
		MTB2 B 2 Bright A_SpawnItemEx("YellowFlare")
		TNT1 A 0 A_SpawnItemEx("MancubusFireTrailparticle")
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Death")
		MTB2 C 2 Bright A_SpawnItemEx("YellowFlare")
		TNT1 A 0 A_SpawnItemEx("MancubusFireTrailparticle")
		TNT1 A 0 A_SpawnItemEx("OldschoolRocketSmokeTrail2")
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,180))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile("ExplosionParticleHeavy",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAA 0 A_CustomMissile("ExplosionParticleVeryFast",0,0,random(0,360),2,random(0,360))
		TNT1 AAAAAAAAAA 0 A_CustomMissile("BarrelExplosionFire",0,0,random(0,360),2,random(0,360))
		TNT1 AA 0 A_SpawnItemEx("TinyBurningPiece2",random(-25,25),random(-15,15))
		TNT1 AA 0 A_SpawnItemEx("TinyBurningPiece",random(-15,15),random(-15,15))
		TNT1 AAAAA 0 A_CustomMissile("ExplosionSmokeFast22",0,0,random(0,360),2,random(0,360))
		TNT1 A 0 A_SpawnItemEx("ExplosionAirDisplacementEffect")
		TNT1 A 0 A_PlaySound("EXPLOSIO3",CHAN_VOICE)
		TNT1 A 0 Radius_Quake(2,8,0,12,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCraterMed")
		TNT1 A 0 A_SpawnItemEx("DetectCeilCraterMed")
		TNT1 A 2
		TNT1 A 0 A_Explode(10,100)
		TNT1 A 0 A_SpawnItemEx("LiquidExplosionEffectSpawner")
		MANF DEFGHI 2 Bright
		Stop
	}
}

Actor 64FlamethrowerTACTICAL : Brutal64Weapon
{
scale 0.7
Weapon.SlotNumber 9
Weapon.AmmoType "SawGas"
Weapon.AmmoGive 300
Weapon.AmmoUse 1
Inventory.PickupMessage "You got the flamethrower!"
Inventory.PickupSound "WPICK"
Obituary "%o was cremated by %k's flamethrower."
Tag "Flamethrower"
+WEAPON.NOAUTOAIM
+Weapon.Explosive
+WEAPON.ALT_USES_BOTH
+INVENTORY.RESTRICTABSOLUTELY
VisibleToPlayerClass "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
Inventory.ForbiddenTo "64DoomPlayer"
	States
	{
	Spawn:
		WFLM A -1
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		FR4M FGHIJ 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("Flamethrower/Ready",CHAN_AUTO)
		FR4M ABCDE 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		FLMG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlames",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects",0,0)
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		FLMG C 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects",0,0)
		FLMG B 1 bright A_FireCustomMissile("64FlamerFlamesLessEffects")
		TNT1 A 0 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		FLMG D 2 A_ReFire
		FLMG EA 2
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIf(waterlevel > 1,"Ready")
		TNT1 A 0 A_JumpIfInventory("SawGas",10,"FireCannonBall")
		TNT1 A 0 A_Print("Not Enough Ammo!",1)
		Goto Ready
	FireCannonBall:
		TNT1 A 0 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_PlaySound("Flamethrower/Cannon",CHAN_WEAPON)
		FLMG C 1 bright A_FireCustomMissile("64FlamerExploder",0,0,0,-8)
		FLMG B 2 bright A_FireCustomMissile("GunFireSmoke",0,0)
		TNT1 A 0 A_TakeInventory("SawGas",10,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		FLMG DDDD 2 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG DDDD 1 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG EEEE 1 A_FireCustomMissile("GunFireSmoke",0,0)
		FLMG EEEE 2 A_FireCustomMissile("GunFireSmoke",0,0)
		Goto Ready
	MineThrowFlash:
		FR4M FGHIJ 1
		TNT1 A 16
		FR4M ABCDE 1
		Stop
	KickingFlash:
		FR4M FGHI 1
		FR4M J 8
		FR4M IHGF 1
		Stop
	AirKickingFlash:
		FR4M FGHI 1
		FR4M J 10
		FR4M IHGF 1
		Stop
	SlideKickingStart:
		FR4M FGHI 1
		FR4M J 17
	SlideKickingEnd:
		FR4M J 6
		FR4M IHGF 1
		Stop
	}
}