Actor NailgunSpawner : CustomInventory
{
scale 0.82
Tag "Nailgun"
Inventory.PickupMessage "You got the NailGun!"
Inventory.PickupSound "WPICK"
	States
	{
	Spawn:
		NLMG I 2
		TNT1 A 0 ACS_NamedExecuteAlways("DM_SpawnACS")
		NLMG I 2
		TNT1 A 0 A_JumpIfInventory("ISDM",1,"DMSpawn")
		NLMG I -1
		Stop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("IsTacticalClass",1,"Pickup2")
		TNT1 A 0 A_JumpIfInventory("IsModern",1,"Pickup2")
		TNT1 A 0 A_GiveInventory("64Nailgun",1)
		Stop
	Pickup2:
		TNT1 A 0 A_GiveInventory("64NailgunTACTICAL",1)
		Stop
	DMSpawn:
		TNT1 A 0 A_ChangeFlag("THRUACTORS",TRUE)
		TNT1 A 0 A_SpawnItemEx("TeleportationFog")
		TNT1 A 0 A_SpawnItemEx("64Nailgun")
		TNT1 A 0 A_SpawnItemEx("64NailgunTACTICAL")
		TNT1 A 1050
		Loop
	}
}

Actor 64Nailgun : DoomWeapon
{
Scale 0.82
Weapon.AmmoUse 1
Weapon.AmmoGive 25
Weapon.AmmoType "64NailAmmo"
Weapon.SlotNumber 6
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the NailGun!"
Obituary "%o was nailed by %k."
Tag "Nailgun"
VisibleToPlayerClass "64DoomPlayer"
Inventory.ForbiddenTo "Tactical64DoomPlayer","Tactical64DoomPlayer_p","Modern"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOAIM
	States
	{
	Spawn:
		NLMG I -1
		Stop
	Select:
		NLMG A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",0)
		NLMG A 1 A_Lower
		Loop
	Ready:
		NLMG A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"FireNail2")
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1)
		NLMF A 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),1,3,-10,0,Random(2,-2))
		NLMF BC 1
		NLMF C 1
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	FireNail2:
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1)
		NLMF D 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),1,-3,-10,0,Random(2,-2))
		NLMF EF 1
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	}
}

Actor 64NailgunTACTICAL : Brutal64Weapon
{
Scale 0.82
Weapon.AmmoUse1 1
Weapon.AmmoGive1 25
Weapon.AmmoType1 "64NailAmmo"
Weapon.SlotNumber 6
Inventory.PickupSound "WPICK"
Inventory.PickupMessage "You got the NailGun!"
Obituary "%o was nailed by %k."
Tag "Nailgun"
+INVENTORY.RESTRICTABSOLUTELY
+WEAPON.NOAUTOAIM
+WEAPON.ALT_USES_BOTH
-WEAPON.AMMO_CHECKBOTH
-WEAPON.NOAUTOFIRE
	States
	{
	Spawn:
		NLMG I -1
		Stop
	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",0)
		TNT1 A 0 A_Takeinventory("WeaponSpecialAction",0)
		TNT1 A 0 A_Takeinventory("TossMine",0)
		TNT1 A 0 A_Takeinventory("PlaceMine",0)
		TNT1 A 0 A_Takeinventory("Kicking",0)
		NLSL EFGH 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 0 A_PlaySound("NAILPKUP",CHAN_AUTO)
		NLSL ABCD 1 Offset(0,32)
		TNT1 A 0 A_Raise
		Wait
	v1Ready:
	Ready:
		NLMG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("TossMine",1,"TossMine")
		TNT1 A 0 A_JumpIfInventory("PlaceMine",1,"PlaceMine")
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"FireNail2")
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1)
		NLMF A 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),1,3,-10,0,Random(2,-2))
		NLMF B 1
		NLMF C 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	FireNail2:
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1)
		NLMF D 1 A_FireCustomMissile("64PlayerNail",Random(4,-4),1,-3,-10,0,Random(2,-2))
		NLMF E 1
		NLMF F 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	Reload:
		NLFI ABCDEFGHJK 1 A_WeaponReady
		TNT1 A 0 A_Jump(64,"AltInspect")
		NLFI LMN 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget1",CHAN_WEAPON)
		NLFI OPQRRRR 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget2",CHAN_WEAPON)
		NLFI STUVXYDCBA 1 A_WeaponReady
		Goto Ready
	AltInspect:
		NLF2 AB 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget1",CHAN_WEAPON)
		NLF2 CDEFGGGGG 1 A_WeaponReady
		TNT1 A 0 A_PlaySound("Weapons/NailFidget2",CHAN_WEAPON)
		NLF2 HIJKLMNOP 1 A_WeaponReady
		NLFI DCBA 1 A_WeaponReady
		Goto Ready
	AltFire:
		TNT1 A 0 A_JumpIfInventory("64NailAmmo",3,"LavaNail1")
		TNT1 A 0 A_Print("Not Enough Nails!")
		Goto Ready
	LavaNail1:
		TNT1 A 0 A_TakeInventory("64NailAmmo",3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("Weapons/NailMGFire",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("IsCrouching",1,3)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,40)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",20,5,22)
		NLMG A 0 A_JumpIfInventory("NailgunFire2ndCannon",1,"LavaNail2")
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,1)
		NLMF G 2 Bright A_FireCustomMissile("64PlayerLavaNail",Random(8,-8),0,3,-10,0,Random(4,-4))
		NLMF H 1 Bright
		NLMF C 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("NailgunFire2ndCannon",1)
		Goto Ready
	LavaNail2:
		TNT1 AA 0 A_FireCustomMissile("GunFireSmoke",0,0,-1)
		NLMF I 2 Bright A_FireCustomMissile("64PlayerLavaNail",Random(8,-8),0,-3,-10,0,Random(4,-4))
		NLMF J 1 Bright
		NLMF F 1 A_SetPitch(pitch-1.2)
		TNT1 A 0 A_ZoomFactor(0.98)
		NLMG A 1 A_SetPitch(pitch+1.2)
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_TakeInventory("NailgunFire2ndCannon",1)
		Goto Ready
	MineThrowFlash:
		NLSL EFGH 1
		TNT1 A 18
		NLSL ABCD 1
		Stop
	KickingFlash:
		NLMG BCDE 1
		NLMG F 8
		NLMG EDCB 1
		Stop
	SlideKickingStart:
		NLMG BCDE 1
		NLMG F 18
	SlideKickingEnd:
		NLMG F 3
		NLMG EDCB 1
		Stop
	}
}

Actor 64NailAmmo : Ammo
{
Scale 0.7
Radius 16
Height 8
Inventory.PickupMessage "Picked up some nails."
Inventory.Icon "NBOXA0"
Inventory.PickupSound "IPICK"
Inventory.Amount 15
Inventory.MaxAmount 300
Tag "Nail Clip"
Ammo.BackpackAmount 15
Ammo.BackpackMaxAmount 600
	States
	{
	Spawn:
		NBOX A -1
		Stop
	}
}

ACTOR 64NailCrate : 64NailAmmo
{
Scale 1.0
Inventory.PickupMessage "Picked up a crate of nails."
Inventory.Amount 40
Tag "Nail Box"
	States
	{
	Spawn:
		NBOX B -1
		Stop
	}
}

Actor 64PlayerNail
{
Scale 0.5
Radius 3
Height 3
Speed 44
Damage 4
SeeSound ""
DeathSound ""
Decal "BulletDecalNew1"
DamageType "CutLess"
PROJECTILE
+BLOODSPLATTER
	States
	{
	Spawn:
		DART A 1 NoDelay A_SpawnItemEx("64NailBlur",0,0,0,1,0,0,180,128)
		TNT1 A 0 A_PlaySound("Weapons/NailFlight",CHAN_AUTO)
		Loop
	Crash:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/NailHit",CHAN_VOICE)
		TNT1 A 1 A_SpawnItemEx("RicoChet")
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("Weapons/NailHitBleed",CHAN_VOICE)
		TNT1 A 1 A_SpawnItemEx("RicoChet")
		Stop
	}
}

Actor 64PlayerLavaNail : 64PlayerNail
{
Damage 8
SeeSound "Weapons/NailMGLava"
DamageType "Plasma"
+FORCEPAIN
	States
	{
	Spawn:
		L4RT A 1 Bright NoDelay A_SpawnItemEx("RedFlareSmall",0,0,0,1,0,0,180,128)
		TNT1 A 0 A_PlaySound("Weapons/NailFlight",CHAN_AUTO)
		Loop
	}
}

Actor 64NailBlur
{
Scale 0.6
Height 8
Radius 1
Damage 0
Speed 0.2
RenderStyle Translucent
Alpha 0.5
+NOGRAVITY
+DROPOFF
+NOTELEPORT
	States
	{
	Spawn:
		DART A 1 A_FadeOut(0.4)
		Wait
	}
}

Actor NailgunFire2ndCannon : Inventory
{
inventory.maxamount 1
}